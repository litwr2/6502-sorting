10 REM V2, BBC MICRO BASIC
20 HIMEM=HIMEM-256:GOSUB6000:HIMEM=HIMEM+256
30 M%=2000:SD=-2
35 DIM AA$(M%):SX%=27:SP%=SX%:DIM SS%(SX%)
40 PRINT"BASIC, ML, OR EXIT? (B/M/E)"
50 K$=GET$:IF K$="E" THENEND
60 C%=0:IF K$="B" THEN80
70 C%=1:IF K$<>"M" THEN40
80 INPUT"NUMBER OF STRINGS TO SORT (0-BACK)",K$:N%=VAL(K$)
90 IF (N%<0)OR(N%>M%) GOTO80
100 IF N%=0 GOTO40
300 DIM E% -1:PRINT"FREE HEAP = ";HIMEM-E%-256
305 PRINT"CHOOSE A METHOD OF FILLING:"
310 PRINT"1. RANDOM"
320 PRINT"2. ORDERED"
330 PRINT"3. REVERSED"
340 PRINT"4. CONSTANT"
350 PRINT"5. 2 RANDOM STRINGS"
360 PRINT"6. 100 RANDOM STRINGS"
370 PRINT"7. SLOWEST FOR HOARE SORT (R)"
380 PRINT"8. SLOWEST FOR HOARE SORT (L)"
390 PRINT"9. EMPTY"
400 PRINT"0. BACK"
410 INPUT K$:R%=VAL(K$)
420 IF (R%<0)OR(R%>9) GOTO300
430 IF R%=0 GOTO80
440 PRINT"CREATING ";N%;
450 ON R% GOSUB1000,1200,1400,1600,1800,2000,2200,2600,2400
460 PRINT"PRINT STRINGS? (Y/N)"
470 K$=GET$:IF K$="N" GOTO500
480 IF K$<>"Y" GOTO470
490 FOR I%=1 TO N%:PRINT;I%;" ";AA$(I%):NEXT
500 PRINT"SORTING..."
510 TIME=0
520 IF C%=1 THEN CALL Q%,N%,AA$(1) ELSE PL%=1:PU%=N%:GOSUB5000
530 T2=TIME:PRINT;N%;" ELEMENTS SORTED IN ";T2/100;" SECONDS"
540 PRINT"CHECKING..."
550 FOR I%=2 TO N%:IF AA$(I%-1)>AA$(I%) THEN PRINT"ERROR: ";I%;": ";AA$(I%-1);" > ";AA$(I%):END
560 PRINT;I%;CHR$(11):NEXT
580 PRINT"PRINT SORTED STRINGS? (Y/N)"
590 K$=GET$:IF K$="N" GOTO650
600 IF K$<>"Y" GOTO590
610 FOR I%=1 TO N%:PRINT;I%;" ";AA$(I%):NEXT
650 PRINT"SORT AGAIN? (Y/N/B)"
660 K$=GET$:IF K$="N" THEN END
670 IF K$="Y" GOTO440
680 IF K$="B" GOTO300
690 GOTO660
1000 PRINT" RANDOM STRINGS"
1010 I%=RND(SD)
1020 FOR I%=1 TO N%
1030 PRINT;I%;CHR$(11)
1050 AA$(I%)=STR$(1000+INT(RND(1)*8000))
1060 NEXT
1070 RETURN
1200 PRINT" ORDERED STRINGS"
1220 FOR I%=1 TO N%
1230 PRINT;I%;CHR$(11)
1250 AA$(I%)=STR$(1000+I%)
1260 NEXT
1270 RETURN
1400 PRINT" REVERSE ORDERED STRINGS"
1420 FOR I%=1 TO N%
1430 PRINT;I%;CHR$(11)
1450 AA$(I%)=STR$(7000-I%)
1460 NEXT
1470 RETURN
1600 PRINT" IDENTICAL STRINGS"
1620 FOR I%=1 TO N%
1630 PRINT;I%;CHR$(11)
1650 AA$(I%)="1234"
1660 NEXT
1670 RETURN
1800 PRINT" STRINGS MIXING 2 STRINGS"
1810 I%=RND(SD)
1820 FOR I%=1 TO N%
1830 PRINT;I%;CHR$(11)
1840 IF RND(1)>=.5 THEN AA$(I%)="1111":GOTO1860
1850 AA$(I%)="2222"
1860 NEXT
1870 RETURN
2000 PRINT" STRINGS MIXING 100 STRINGS"
2010 I%=RND(SD)
2020 FOR I%=1 TO N%
2030 PRINT;I%;CHR$(11)
2050 AA$(I%)=STR$(1000+INT(RND(1)*100))
2060 NEXT
2070 RETURN
2200 PRINT" STRINGS: THE WORST CASE FOR HOARE SORT (R)"
2205 IF (N%AND1)<>0 THEN PRINT"USE AN EVEN NUMBER OF ELEMENTS!":GOTO2270
2210 AA$(1)="1001":AA$(2)="1003":AA$(3)="1005":AA$(4)="1000":AA$(5)="1002":AA$(6)="1004"
2220 FOR I%=3 TO N%/2-1
2230 AA$(I%+I%+1)=AA$(I%+1):AA$(I%+I%+2)=STR$(VAL(AA$(I%+I%))+2):AA$(I%+1)=STR$(I%+I%+1001)
2240 PRINT;I%;CHR$(11)
2250 NEXT
2270 RETURN
2400 PRINT" EMPTY STRINGS"
2420 FOR I%=1 TO N%
2430 PRINT;I%;CHR$(11)
2450 AA$(I%)=""
2460 NEXT
2470 RETURN
2600 PRINT" STRINGS: THE WORST CASE FOR HOARE SORT (L)"
2605 IF (N%AND1)<>0 THEN2205
2610 K%=N%/2
2620 FOR I%=0 TO K%-1
2630 GOSUB2700:AA$(K%-I%)=STR$(P1%+1000):AA$(I%+K%+1)=STR$(I%+I%+1001)
2640 PRINT;I%;CHR$(11)
2650 NEXT
2670 RETURN
2700 IF I%=0 THEN P1%=0:RETURN
2710 IF (K%-I%AND1)=0 THEN P1%=K%-I%:RETURN
2720 PU%=(K%+I%-1)DIV2:GOSUB2730:P1%=P1%+K%-I%+1:RETURN
2730 IF (PU%AND1)=1 THEN P1%=PU%-1:RETURN
2740 SS%(SP%)=PU%:SP%=SP%-1:PU%=PU%DIV2:GOSUB2730:SP%=SP%+1:PU%=SS%(SP%):P1%=PU%+P1%:RETURN
5000 REM NON-RECURSIVE QUICKSORT
5010 P1%=PL%:PJ%=PU%:K%=(P1%+PJ%)/2:K$=AA$(K%)
5020 IF AA$(P1%)<K$ THEN P1%=P1%+1:GOTO5020
5030 IF AA$(PJ%)>K$ THEN PJ%=PJ%-1:GOTO5030
5040 IF P1%>PJ% THEN5070
5050 IF P1%<>PJ% THEN X$=AA$(P1%):AA$(P1%)=AA$(PJ%):AA$(PJ%)=X$
5060 P1%=P1%+1:PJ%=PJ%-1
5070 IF P1%<=PJ% GOTO5020
5080 IF PJ%>K% GOTO5160
5090 IF PL%>=PJ% GOTO5130
5100 SS%(SP%)=PU%:SP%=SP%-1
5110 SS%(SP%)=P1%:SP%=SP%-1
5120 PU%=PJ%:GOTO5010
5130 IF P1%>=PU% GOTO5240
5140 PL%=P1%:GOTO5010
5160 IF P1%>=PU% GOTO5200
5170 SS%(SP%)=PJ%:SP%=SP%-1
5180 SS%(SP%)=PL%:SP%=SP%-1
5190 PL%=P1%:GOTO5010
5200 IF PL%>=PJ% GOTO5240
5230 PU%=PJ%:GOTO5010
5240 IF SP%=SX% THEN RETURN
5250 SP%=SP%+1:PL%=SS%(SP%)
5260 SP%=SP%+1:PU%=SS%(SP%)
5270 GOTO5010
6000 PRINT"LOADING THE SORT ROUTINE...":I%=HIMEM
6010 OSCLI"LOAD QRELOC "+STR$~I%
6040 DIM Q% 617
6050 OSCLI"LOAD EQSORT "+STR$~Q%
6080 CALL I%
6090 RETURN
